package codigo;

import java_cup.runtime.*;
import java.util.ArrayList;
import java.util.List;

parser code
{:
 	private ArrayList<String> regla = new ArrayList<>();
	 private ArrayList<Simbolos> simbolo = new ArrayList<>();
	 
	 /*Entero 16 Bits*/
	private final int max_int = Short.MAX_VALUE;
	/*real 32 bits*/
	private final float max_float = Float.MAX_VALUE;
	/*constantes string no mayor a 30 caracteres*/
	 private final int max_string = 32; /* 32 YA QUE CUENTA LAS COMILLAS*/
	 private ArrayList<String> error = new ArrayList<>();
	 
    /**Metodo al que se llama automaticamente ante algun error sintactico.*/
    public void syntax_error(Symbol s){
       setError("Error en la linea: " + (s.left) + " columna: " + (s.right) + ". "
           + "valor " + s.value );
    }
    
	public void unrecovered_syntax_error(Symbol s) 
	throws java.lang.Exception{
			setError("Error NR en la linea: " + (s.left) + " columna: " + s.right + ". "
            + "valor " + s.value ); 
		}

	
	 
 private void setRegla(String r){
		regla.add(r);
	}
	
	public String getReglas(){
		String s ="";
		for(String l:regla) {
			s +=l + "\n";
		}
		return s;
	};
	
	private void setSimbolos(String l, String t, String ti){
		boolean flag;
		flag = true;
		if(simbolo.size()>0){
			for (Simbolos s:simbolo){
				switch  (t){
					case "ID" :
						if(s.getNombre().equals(l)){
							flag=false;
						}
						break;
					case "CONST_STRING":
						if(s.getNombre().equals(l.substring(1,l.length()-1))){
							flag=false;
						}
						break;
					default :
						if(s.getValor().equals(l)){
							flag=false;
						}
						break;
				}
			}
		}
		if (flag){
			simbolo.add(new Simbolos(l,t,ti));
		}
	}
	
	
	public ArrayList<Simbolos> getSimbolos(){
		return simbolo;
	};
	
	
	private void controlString(String s){
		if (s.length()> max_string ){
			setError(String.format("Tamanio de string superior a 30, tamanio actual: %d %n",(s.length()-2)));
		}	
	}
	
	private void controlReal(String s){
		float r = Float.parseFloat(s);
		if(r < 0 || r> max_float){
			setError(String.format("Valor real fuera de rango %d - %d , valor actual: %d %n",0,max_float,r));
		}
	}
	
	private void controlEntero(String s){
		int i =Integer.parseInt(s);
	 	if(i < 0 || i > max_int){
           setError(String.format("Valor Entero fuera de rango %d - %d, valor actual: %d %n",0,max_int, i));
        }
	}
	
	
	private void setError(String s ){
		error.add(s);
	}
	
	public ArrayList<String> getError(){
		return error;
	}
	
	private void declareID(String i){
		boolean flag=false;
		for (Simbolos s:simbolo){
			if(s.getToken()=="ID" && s.getNombre().equals(i)){
				flag=true;
			}
		}
		if(!flag){
			setError("ERROR ID no declarado " + i);
		}
	}
	

:}

action code
{:
:}


/*Declaracion de Terminales */
terminal ID, ASSIGN, PARENA, PARENC, SUMA, RESTA, DIVI, MULTI, COMA, CORCHA, CORCHC, PUNTOYCOMA, MAYOR, MENOR, MAYOROIGUAL, MENOROIGUAL, DISTINTO, IGUAL;
terminal PR_DECLARE, PR_ENDDECLARE, PR_AND, PR_OR, PR_IF, PR_ELSE, PR_ENDIF, PR_WRITE, PR_PROGRAM, PR_ENDPROGRAM, PR_WHILE, PR_ENDWHILE, PR_PROMR;
terminal CONST_INT, PR_INT, PR_STRING, PR_FLOAT, CONST_STRING, CONST_FLOAT,EXCEP;

/*Declaracion de No Terminales*/
non terminal pgm, declare, listavariable, lista, assigvar, tipo;
non terminal constante, program, asignacion, valor, expresion, termino, factor;
non terminal sentencias, sentencia, mostrar, compara, comp, expresionlog, if, while, linea, promr, valores, listaval;


start with pgm;

pgm ::=  program {: setRegla("pgm Regla 0 Compila correctamente llegando al simbolo start"); :};
program ::= declare sentencias {: setRegla(" [Regla 1] declare programa"); :};

declare ::= PR_DECLARE lista PR_ENDDECLARE {: setRegla("[Regla 2] seccion declare"); :}
			| error PR_ENDDECLARE;
			
lista ::= listavariable {:setRegla("[Regla 3] listavariable");:};
lista ::= listavariable lista {:setRegla("[Regla 4] lista");:};
listavariable ::= CORCHA ID:i assigvar tipo:ti CORCHC  {: RESULT = i; RESULT = ti; setRegla("[Regla 5] listavariable apertura " + i + " - " + ti); setSimbolos((String)i,"ID",(String) ti);:};
assigvar ::= COMA ID:i assigvar tipo:ti COMA {: RESULT = i; RESULT = ti; setRegla("[Regla 6] assigvar " + i +" - " + ti ); setSimbolos((String)i,"ID",(String) ti); :};
assigvar ::= CORCHC ASSIGN CORCHA {: setRegla("[Regla 6] assigvar fin ");:};
tipo ::= PR_INT:ti  {:setRegla("[Regla 7] PR_INT"); RESULT = ti;:} 
		| PR_FLOAT:ti {:setRegla("[Regla 8] PR_FLOAT"); RESULT = ti;:} 
		| PR_STRING:ti {:setRegla("[Regla 9]  PR_STRING");RESULT = ti;:};

sentencias ::= PR_PROGRAM sentencia PR_ENDPROGRAM{:setRegla("[Regla 10] sentencias"); :}
				| error sentencia
				| error PR_ENDPROGRAM;
sentencia ::= sentencia linea{:setRegla("[Regla 11] sentencia"); :}
				| error linea;
sentencia ::= linea {:setRegla("[Regla 12] sentencia"); :};
linea ::= asignacion {:setRegla("[Regla 13] linea asignacion"); :} 
		| mostrar {:setRegla("[Regla 14] linea mostrar"); :} 
		| if {:setRegla("[Regla 15] linea if"); :} 
		| while {:setRegla("[Regla 16] linea while"); :};
		
asignacion ::= ID:i ASSIGN valor PUNTOYCOMA {:declareID((String) i); setRegla("[Regla 17] asignacion " + i); RESULT = i;:} ;
valor ::= CONST_STRING:s {:setRegla("[Regla 18] valor CONST_STRING"); controlString((String) s); setSimbolos((String) s,"CONST_STRING",""); :} 
		| expresion {:setRegla("[Regla 19] valor expresion"); :};
mostrar ::= PR_WRITE constante PUNTOYCOMA {:setRegla("[Regla 20] mostra constante"); :}
		| PR_WRITE ID:i PUNTOYCOMA {: declareID((String) i); setRegla("[Regla 21] mostra ID"); RESULT=i; :};

expresionlog ::=  comp PR_AND comp {:setRegla("[Regla 22] expresionlog PR_AND"); :}
		|comp PR_OR comp {:setRegla("[Regla 23] expresionlog PR_OR"); :} 
		|comp {:setRegla("[Regla 24] comp"); :}
		| error PR_OR
		| error PR_AND;
comp ::= expresion compara expresion {:setRegla("[Regla 25] expresion compara expresion"); :} 
		| expresion compara CONST_STRING:s {:setRegla("[Regla 26] expresion compara CONST_STRING"); controlString((String) s); setSimbolos((String) s,"CONST_STRING",""); :} 
		| CONST_STRING:s compara expresion {:setRegla("[Regla 27] CONST_STRING compara expresion"); controlString((String) s); setSimbolos((String) s,"CONST_STRING",""); :}
		| error compara;
compara ::= MAYOR {:setRegla("[Regla 28] MAYOR"); :}
		| MENOR {:setRegla("[Regla 29] MENOR"); :}
		| IGUAL {:setRegla("[Regla 30] IGUAL"); :}
		| DISTINTO {:setRegla("[Regla 31] DISTINTO"); :}
		| MENOROIGUAL {:setRegla("[Regla 32] MENOROIGUAL"); :}
		| MAYOROIGUAL {:setRegla("[Regla 33] MAYOROIGUAL"); :};

if ::= PR_IF PARENA expresionlog PARENC sentencia PR_ENDIF PUNTOYCOMA {:setRegla("[Regla 34] IF"); :} 
		| PR_IF PARENA expresionlog PARENC sentencia PR_ELSE sentencia PR_ENDIF PUNTOYCOMA {:setRegla("[Regla 35] IF ELSE"); :};
	
while ::= PR_WHILE PARENA expresionlog PARENC sentencia PR_ENDWHILE PUNTOYCOMA {:setRegla("[Regla 36] WHILE"); :};
promr ::= PR_PROMR PARENA ID PUNTOYCOMA MENOROIGUAL PUNTOYCOMA listaval PARENC {:setRegla("[Regla 37] promr"); :}
			| error PARENC;
listaval ::= CORCHA CORCHC {:setRegla("[Regla 38] listaval vacia"); :};
listaval ::= CORCHA valores CORCHC {:setRegla("[Regla 39] listaval"); :};
valores ::= valores COMA CONST_INT:i   {:setRegla("[Regla 40] valores ,CONST_INT"); controlEntero((String) i); setSimbolos((String) i,"CONST_INT",""); :}
			| error COMA;
valores ::= CONST_INT:i {:setRegla("[Regla 41] valores CONST_INT"); controlEntero((String)i); setSimbolos((String) i,"CONST_INT","");:};


constante ::= CONST_INT:i {:setRegla("[Regla 42] CONST_INT");  controlEntero((String)i); setSimbolos((String) i,"CONST_INT",""); :} 
		| CONST_STRING:s {:setRegla("[Regla 43] CONST_STRING"); controlString((String )s); setSimbolos((String) s,"CONST_STRING",""); :} 
		| CONST_FLOAT:f {:setRegla("[Regla 44] CONST_FLOAT"); controlReal((String) f); setSimbolos((String) f,"CONST_FLOAT",""); :};   
expresion ::= expresion SUMA termino {:setRegla("[Regla 45] E+T"); :} 
		| expresion RESTA termino {:setRegla("[Regla 46] E-T"); :} 
		| termino {:setRegla("[Regla 47] T"); :};
termino ::= termino MULTI factor {:setRegla("[Regla 48] T*F"); :} 
		| termino DIVI factor {:setRegla("[Regla 49] T/F"); :} 
		| factor{:setRegla("[Regla 50] F"); :};
factor ::= CONST_INT:i {:setRegla("[Regla 51] CONST_INT es F" + i ); RESULT = i; controlEntero((String)i); setSimbolos((String) i,"CONST_INT","");:} 
		| CONST_FLOAT:f {:setRegla( "[Regla 52] CONST_FLOAT es F"); controlReal((String) f);  setSimbolos((String) f,"CONST_FLOAT","");:} 
		| ID:i {: declareID((String) i); setRegla("[Regla 53] ID es F " + i); RESULT=i; :}
	 	| PARENA expresion PARENC{:setRegla("[Regla 54] (E)"); :}
	 	| promr {:setRegla("[Regla 55]  promr es F"); :}; 
             




